plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    // DB: SqlDelight
    id "app.cash.sqldelight" version "2.0.0-alpha05"

    // DI: Hilt
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'

    // Safe Args Kotlin version
    //
    // If we need to support Java then use this instead
    //   id 'androidx.navigation.safeargs'
    id 'androidx.navigation.safeargs.kotlin'

    // Use Parcelize to generate Parcelable boiler plate code.
    id "kotlin-parcelize"
}

sqldelight {
    databases {
        Database { // This will be the name of the generated database class.
            packageName = "com.franklinharper.jpmc.nycschools"
        }
    }
}

android {
    namespace 'com.franklinharper.jpmc.nycschools'
    compileSdk 33

    defaultConfig {
        applicationId "com.franklinharper.jpmc.nycschools"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    def navVersion = "2.5.3"
    def coroutinesVersion = "1.6.4"


    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation("com.laimiux.lce:lce:0.3.1")

    // DB: SqlDelight
    implementation "app.cash.sqldelight:android-driver:2.0.0-alpha05"

    // DI: Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    // This is the JVM SQLite driver. Using it - instead of the Android driver - enables testing the
    // database without needing an Android emulator or physical device.
    testImplementation "app.cash.sqldelight:sqlite-driver:2.0.0-alpha05"

    // Android Test dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

// Required by Hilt: Allow references to generated code.
kapt {
    correctErrorTypes true
}